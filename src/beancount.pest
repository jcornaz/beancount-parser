beancount_file = { SOI ~ (directive | blank_line | comment_line)* ~ EOI }

directive = _{ directive_body ~ space* ~ inline_comment? ~ (NEWLINE | EOI) }
directive_body = _
    { transaction
    | balance_assertion
    | open_directive
    | close_directive
    | price_directive
    | commodity
    | event
    | option
    }
open_directive = { date ~ space+ ~ "open" ~ space+ ~ account ~ space* ~ currency_list? }
close_directive = { date ~ space+ ~ "close" ~ space+ ~ account }
commodity = { date ~ space+ ~ "commodity" ~ space+ ~ currency }
balance_assertion = { date ~ space+ ~ "balance" ~ space+ ~ account ~ space+ ~ amount }
event = { date ~ space+ ~ "event" ~ space+ ~ string ~ space+ ~ string }
price_directive = { date ~ space+ ~ "price" ~ space+ ~ currency ~ space+ ~ amount }
option = { "option" ~ space+ ~ string ~ space+ ~ string }
currency_list = _{ currency ~ (comma_separator ~ currency)* }

transaction =
    { date
    ~ space+
    ~ (transaction_flag | "txn")
    ~ (space+ ~ payee_and_narration)?
    ~ (space+ ~ tags)?
    ~ space*
    ~ inline_comment?
    ~ postings
    }
transaction_flag = { "*" | "!" }
payee_and_narration = _{ (payee ~ space+ ~ narration | narration) }
payee = { string }
narration = { string }
string = { "\"" ~ string_content ~  "\"" }
string_content = { (!"\"" ~ ANY)* }
tags = { tag ~ (space+ ~ tag)* }
tag = { "#" ~ (!(space | NEWLINE | "#" ) ~ ANY)+ }

postings = { (NEWLINE ~ posting)* }
posting =
    { space*
    ~ (transaction_flag ~ space*)?
    ~ account
    ~ (space+ ~ amount ~ (space+ ~ lot)? ~ (space* ~ price)?)?
    ~ space*
    ~ inline_comment?
    }
lot = { "{" ~ space* ~ ((amount ~ (space* ~ "," ~ space* ~ date)? | date))? ~ space* ~ "}" }
price = { price_type ~ space* ~ amount }
price_type = { "@@" | "@" }

amount = { expression ~ space+ ~ currency }

expression = _{ exp_p2 }
exp_p0 = { value | "(" ~ space* ~ expression ~ space* ~ ")" }
exp_p1 = { exp_p0 ~ (space* ~ op_p1 ~ space* ~ exp_p0)* }
exp_p2 = { exp_p1 ~ (space* ~ op_p2 ~ space* ~ exp_p1)* }
op_p1 = { "*" | "/" }
op_p2 = { "+" | "-" }

value = { ("-" | "+")? ~ (ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)? | "." ~ ASCII_DIGIT+) }
currency = { ASCII_ALPHA_UPPER ~ (ASCII_ALPHA_UPPER | ASCII_DIGIT | "'" | "-" | "." | "_")* }

account = { account_type ~ (":" ~ account_component)* }
account_type = { "Assets" | "Liabilities" | "Equity" | "Income" | "Expenses" }
account_component = { (ASCII_ALPHANUMERIC | "-")+ }

date = { year ~ "-" ~ month ~ "-" ~ day }
year = { ASCII_DIGIT{4} }
month = { ASCII_DIGIT{2} }
day = { ASCII_DIGIT{2} }

blank_line = _{ !EOI ~ space* ~ (NEWLINE | EOI) }
comment_line = _{ space* ~ !(date | keyword | (account_type ~ ":") | EOI) ~ comment ~ (NEWLINE | EOI) }
keyword = _{ "option" | "pushtag"  | "plugin" | "include" }
inline_comment = _{ ";"+ ~ space* ~ comment }
comment = { (!NEWLINE ~ ANY)* }

space = _{ " " | "\t" }
comma_separator = _{ space* ~ "," ~ space* }
